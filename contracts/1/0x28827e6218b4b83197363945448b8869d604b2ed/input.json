{
  "language": "Solidity",
  "settings": {
    "libraries": {},
    "metadata": {
      "useLiteralContent": true
    },
    "optimizer": {
      "enabled": true,
      "runs": 200
    },
    "remappings": [],
    "outputSelection": {
      "*": {
        "*": [
          "*"
        ],
        "": [
          "*"
        ]
      }
    }
  },
  "sources": {
    "browser/ConverterRamp.sol": {
      "content": "/**\n *Submitted for verification at Etherscan.io on 2020-06-11\n*/\n\n// File: contracts/interfaces/IERC20.sol\n\npragma solidity ^0.6.6;\n\n\ninterface IERC20 {\n    event Approval(address indexed owner, address indexed spender, uint value);\n    event Transfer(address indexed from, address indexed to, uint value);\n\n    function name() external view returns (string memory);\n    function symbol() external view returns (string memory);\n    function decimals() external view returns (uint8);\n    function totalSupply() external view returns (uint);\n    function balanceOf(address owner) external view returns (uint);\n    function allowance(address owner, address spender) external view returns (uint);\n\n    function approve(address spender, uint value) external returns (bool);\n    function transfer(address to, uint value) external returns (bool);\n    function transferFrom(address from, address to, uint value) external returns (bool);\n}\n\n// File: contracts/utils/SafeMath.sol\n\npragma solidity ^0.6.6;\n\n\nlibrary SafeMath {\n    using SafeMath for uint256;\n\n    function add(uint256 x, uint256 y) internal pure returns (uint256) {\n        uint256 z = x + y;\n        require(z >= x, \"Add overflow\");\n        return z;\n    }\n\n    function sub(uint256 x, uint256 y) internal pure returns (uint256) {\n        require(x >= y, \"Sub overflow\");\n        return x - y;\n    }\n\n    function mul(uint256 x, uint256 y) internal pure returns (uint256) {\n        if (x == 0) {\n            return 0;\n        }\n\n        uint256 z = x * y;\n        require(z/x == y, \"Mult overflow\");\n        return z;\n    }\n\n    function div(uint256 x, uint256 y) internal pure returns (uint256) {\n        require(y != 0, \"Div by zero\");\n        return x / y;\n    }\n\n    function multdiv(uint256 x, uint256 y, uint256 z) internal pure returns (uint256) {\n        require(z != 0, \"div by zero\");\n        return x.mul(y) / z;\n    }\n}\n\n// File: contracts/interfaces/IERC173.sol\n\npragma solidity ^0.6.6;\n\n\n/// @title ERC-173 Contract Ownership Standard\n/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-173.md\n///  Note: the ERC-165 identifier for this interface is 0x7f5828d0\ninterface IERC173 {\n    /// @dev This emits when ownership of a contract changes.\n    event OwnershipTransferred(address indexed _previousOwner, address indexed _newOwner);\n\n    /// @notice Get the address of the owner\n    /// @return The address of the owner.\n    function owner() external view returns (address);\n\n    /// @notice Set the address of the new owner of the contract\n    /// @param _newOwner The address of the new owner of the contract\n    function transferOwnership(address _newOwner) external;\n}\n\n// File: contracts/utils/Ownable.sol\n\npragma solidity ^0.6.6;\n\n\n\ncontract Ownable is IERC173 {\n    address internal _owner;\n\n    modifier onlyOwner() {\n        require(msg.sender == _owner, \"The owner should be the sender\");\n        _;\n    }\n\n    constructor() public {\n        _owner = msg.sender;\n        emit OwnershipTransferred(address(0x0), msg.sender);\n    }\n\n    function owner() external override view returns (address) {\n        return _owner;\n    }\n\n    /**\n        @dev Transfers the ownership of the contract.\n\n        @param _newOwner Address of the new owner\n    */\n    function transferOwnership(address _newOwner) external override onlyOwner {\n        require(_newOwner != address(0), \"0x0 Is not a valid owner\");\n        emit OwnershipTransferred(_owner, _newOwner);\n        _owner = _newOwner;\n    }\n}\n\n// File: contracts/interfaces/rcn/ICosigner.sol\n\npragma solidity ^0.6.6;\n\n\ninterface ICosigner {\n    function cost(\n        address engine,\n        uint256 index,\n        bytes calldata data,\n        bytes calldata oracleData\n    ) external view returns (uint256);\n}\n\n// File: contracts/interfaces/rcn/IModel.sol\n\npragma solidity ^0.6.6;\n\n\n/**\n    The abstract contract Model defines the whole lifecycle of a debt on the DebtEngine.\n\n    Models can be used without previous approbation, this is meant\n    to avoid centralization on the development of RCN; this implies that not all models are secure.\n    Models can have back-doors, bugs and they have not guarantee of being autonomous.\n\n    The DebtEngine is meant to be the User of this model,\n    so all the methods with the ability to perform state changes should only be callable by the DebtEngine.\n\n    All models should implement the 0xaf498c35 interface.\n\n    @author Agustin Aguilar\n*/\ninterface IModel {\n    // ///\n    // Events\n    // ///\n\n    /**\n        @dev This emits when create a new debt.\n    */\n    event Created(bytes32 indexed _id);\n\n    /**\n        @dev This emits when the status of debt change.\n\n        @param _timestamp Timestamp of the registry\n        @param _status New status of the registry\n    */\n    event ChangedStatus(bytes32 indexed _id, uint256 _timestamp, uint256 _status);\n\n    /**\n        @dev This emits when the obligation of debt change.\n\n        @param _timestamp Timestamp of the registry\n        @param _debt New debt of the registry\n    */\n    event ChangedObligation(bytes32 indexed _id, uint256 _timestamp, uint256 _debt);\n\n    /**\n        @dev This emits when the frequency of debt change.\n\n        @param _timestamp Timestamp of the registry\n        @param _frequency New frequency of each installment\n    */\n    event ChangedFrequency(bytes32 indexed _id, uint256 _timestamp, uint256 _frequency);\n\n    /**\n        @param _timestamp Timestamp of the registry\n    */\n    event ChangedDueTime(bytes32 indexed _id, uint256 _timestamp, uint256 _status);\n\n    /**\n        @param _timestamp Timestamp of the registry\n        @param _dueTime New dueTime of each installment\n    */\n    event ChangedFinalTime(bytes32 indexed _id, uint256 _timestamp, uint64 _dueTime);\n\n    /**\n        @dev This emits when the call addDebt function.\n\n        @param _amount New amount of the debt, old amount plus added\n    */\n    event AddedDebt(bytes32 indexed _id, uint256 _amount);\n\n    /**\n        @dev This emits when the call addPaid function.\n\n        If the registry is fully paid on the call and the amount parameter exceeds the required\n            payment amount, the event emits the real amount paid on the payment.\n\n        @param _paid Real amount paid\n    */\n    event AddedPaid(bytes32 indexed _id, uint256 _paid);\n\n    // ///\n    // Meta\n    // ///\n\n    /**\n        @return Identifier of the model\n    */\n    function modelId() external view returns (bytes32);\n\n    /**\n        Returns the address of the contract used as Descriptor of the model\n\n        @dev The descriptor contract should follow the ModelDescriptor.sol scheme\n\n        @return Address of the descriptor\n    */\n    function descriptor() external view returns (address);\n\n    /**\n        If called for any address with the ability to modify the state of the model registries,\n            this method should return True.\n\n        @dev Some contracts may check if the DebtEngine is\n            an operator to know if the model is operative or not.\n\n        @param operator Address of the target request operator\n\n        @return canOperate True if operator is able to modify the state of the model\n    */\n    function isOperator(address operator) external view returns (bool canOperate);\n\n    /**\n        Validates the data for the creation of a new registry, if returns True the\n            same data should be compatible with the create method.\n\n        @dev This method can revert the call or return false, and both meant an invalid data.\n\n        @param data Data to validate\n\n        @return isValid True if the data can be used to create a new registry\n    */\n    function validate(bytes calldata data) external view returns (bool isValid);\n\n    // ///\n    // Getters\n    // ///\n\n    /**\n        Exposes the current status of the registry. The possible values are:\n\n        1: Ongoing - The debt is still ongoing and waiting to be paid\n        2: Paid - The debt is already paid and\n        4: Error - There was an Error with the registry\n\n        @dev This method should always be called by the DebtEngine\n\n        @param id Id of the registry\n\n        @return status The current status value\n    */\n    function getStatus(bytes32 id) external view returns (uint256 status);\n\n    /**\n        Returns the total paid amount on the registry.\n\n        @dev it should equal to the sum of all real addPaid\n\n        @param id Id of the registry\n\n        @return paid Total paid amount\n    */\n    function getPaid(bytes32 id) external view returns (uint256 paid);\n\n    /**\n        If the returned amount does not depend on any interactions and only on the model logic,\n            the defined flag will be True; if the amount is an estimation of the future debt,\n            the flag will be set to False.\n\n        If timestamp equals the current moment, the defined flag should always be True.\n\n        @dev This can be a gas-intensive method to call, consider calling the run method before.\n\n        @param id Id of the registry\n        @param timestamp Timestamp of the obligation query\n\n        @return amount Amount pending to pay on the given timestamp\n        @return defined True If the amount returned is fixed and can't change\n    */\n    function getObligation(bytes32 id, uint64 timestamp) external view returns (uint256 amount, bool defined);\n\n    /**\n        The amount required to fully paid a registry.\n\n        All registries should be payable in a single time, even when it has multiple installments.\n\n        If the registry discounts interest for early payment, those discounts should be\n            taken into account in the returned amount.\n\n        @dev This can be a gas-intensive method to call, consider calling the run method before.\n\n        @param id Id of the registry\n\n        @return amount Amount required to fully paid the loan on the current timestamp\n    */\n    function getClosingObligation(bytes32 id) external view returns (uint256 amount);\n\n    /**\n        The timestamp of the next required payment.\n\n        After this moment, if the payment goal is not met the debt will be considered overdue.\n\n            The getObligation method can be used to know the required payment on the future timestamp.\n\n        @param id Id of the registry\n\n        @return timestamp The timestamp of the next due time\n    */\n    function getDueTime(bytes32 id) external view returns (uint256 timestamp);\n\n    // ///\n    // Metadata\n    // ///\n\n    /**\n        If the loan has multiple installments returns the duration of each installment in seconds,\n            if the loan has not installments it should return 1.\n\n        @param id Id of the registry\n\n        @return frequency Frequency of each installment\n    */\n    function getFrequency(bytes32 id) external view returns (uint256 frequency);\n\n    /**\n        If the loan has multiple installments returns the total of installments,\n            if the loan has not installments it should return 1.\n\n        @param id Id of the registry\n\n        @return installments Total of installments\n    */\n    function getInstallments(bytes32 id) external view returns (uint256 installments);\n\n    /**\n        The registry could be paid before or after the date, but the debt will always be\n            considered overdue if paid after this timestamp.\n\n        This is the estimated final payment date of the debt if it is always paid on each exact dueTime.\n\n        @param id Id of the registry\n\n        @return timestamp Timestamp of the final due time\n    */\n    function getFinalTime(bytes32 id) external view returns (uint256 timestamp);\n\n    /**\n        Similar to getFinalTime returns the expected payment remaining if paid always on the exact dueTime.\n\n        If the model has no interest discounts for early payments,\n            this method should return the same value as getClosingObligation.\n\n        @param id Id of the registry\n\n        @return amount Expected payment amount\n    */\n    function getEstimateObligation(bytes32 id) external view returns (uint256 amount);\n\n    // ///\n    // State interface\n    // ///\n\n    /**\n        Creates a new registry using the provided data and id, it should fail if the id already exists\n            or if calling validate(data) returns false or throws.\n\n        @dev This method should only be callable by an operator\n\n        @param id Id of the registry to create\n        @param data Data to construct the new registry\n\n        @return success True if the registry was created\n    */\n    function create(bytes32 id, bytes calldata data) external returns (bool success);\n\n    /**\n        If the registry is fully paid on the call and the amount parameter exceeds the required\n            payment amount, the method returns the real amount used on the payment.\n\n        The payment taken should always be the same as the requested unless the registry\n            is fully paid on the process.\n\n        @dev This method should only be callable by an operator\n\n        @param id If of the registry\n        @param amount Amount to pay\n\n        @return real Real amount paid\n    */\n    function addPaid(bytes32 id, uint256 amount) external returns (uint256 real);\n\n    /**\n        Adds a new amount to be paid on the debt model,\n            each model can handle the addition of more debt freely.\n\n        @dev This method should only be callable by an operator\n\n        @param id Id of the registry\n        @param amount Debt amount to add to the registry\n\n        @return added True if the debt was added\n    */\n    function addDebt(bytes32 id, uint256 amount) external returns (bool added);\n\n    // ///\n    // Utils\n    // ///\n\n    /**\n        Runs the internal clock of a registry, this is used to compute the last changes on the state.\n            It can make transactions cheaper by avoiding multiple calculations when calling views.\n\n        Not all models have internal clocks, a model without an internal clock should always return false.\n\n        Calls to this method should be possible from any address,\n            multiple calls to run shouldn\"t affect the internal calculations of the model.\n\n        @dev If the call had no effect the method would return False,\n            that is no sign of things going wrong, and the call shouldn\"t be wrapped on a require\n\n        @param id If of the registry\n\n        @return effect True if the run performed a change on the state\n    */\n    function run(bytes32 id) external returns (bool effect);\n}\n\n// File: contracts/interfaces/rcn/IRateOracle.sol\n\npragma solidity ^0.6.6;\n\n\n/// @dev Defines the interface of a standard Diaspore RCN Oracle,\n/// The contract should also implement it is ERC165 interface: 0xa265d8e0\n/// @notice Each oracle can only support one currency\n/// @author Agustin Aguilar\ninterface IRateOracle {\n    function readSample(bytes calldata _data) external returns (uint256 _tokens, uint256 _equivalent);\n}\n\n// File: contracts/interfaces/rcn/IDebtEngine.sol\n\npragma solidity ^0.6.6;\n\n\n\n\ninterface IDebtEngine {\n    function debts(\n        bytes32 _id\n    ) external view returns(\n        bool error,\n        uint128 balance,\n        IModel model,\n        address creator,\n        IRateOracle oracle\n    );\n\n    function create(\n        IModel _model,\n        address _owner,\n        address _oracle,\n        bytes calldata _data\n    ) external returns (bytes32 id);\n\n    function create2(\n        IModel _model,\n        address _owner,\n        address _oracle,\n        uint256 _salt,\n        bytes calldata _data\n    ) external returns (bytes32 id);\n\n    function create3(\n        IModel _model,\n        address _owner,\n        address _oracle,\n        uint256 _salt,\n        bytes calldata _data\n    ) external returns (bytes32 id);\n\n    function buildId(\n        address _creator,\n        uint256 _nonce\n    ) external view returns (bytes32);\n\n    function buildId2(\n        address _creator,\n        address _model,\n        address _oracle,\n        uint256 _salt,\n        bytes calldata _data\n    ) external view returns (bytes32);\n\n    function buildId3(\n        address _creator,\n        uint256 _salt\n    ) external view returns (bytes32);\n\n    function pay(\n        bytes32 _id,\n        uint256 _amount,\n        address _origin,\n        bytes calldata _oracleData\n    ) external returns (uint256 paid, uint256 paidToken);\n\n    function payToken(\n        bytes32 id,\n        uint256 amount,\n        address origin,\n        bytes calldata oracleData\n    ) external returns (uint256 paid, uint256 paidToken);\n\n    function payBatch(\n        bytes32[] calldata _ids,\n        uint256[] calldata _amounts,\n        address _origin,\n        address _oracle,\n        bytes calldata _oracleData\n    ) external returns (uint256[] memory paid, uint256[] memory paidTokens);\n\n    function payTokenBatch(\n        bytes32[] calldata _ids,\n        uint256[] calldata _tokenAmounts,\n        address _origin,\n        address _oracle,\n        bytes calldata _oracleData\n    ) external returns (uint256[] memory paid, uint256[] memory paidTokens);\n\n    function withdraw(\n        bytes32 _id,\n        address _to\n    ) external returns (uint256 amount);\n\n    function withdrawPartial(\n        bytes32 _id,\n        address _to,\n        uint256 _amount\n    ) external returns (bool success);\n\n    function withdrawBatch(\n        bytes32[] calldata _ids,\n        address _to\n    ) external returns (uint256 total);\n\n    function transferFrom(address _from, address _to, uint256 _assetId) external;\n\n    function getStatus(bytes32 _id) external view returns (uint256);\n}\n\n// File: contracts/interfaces/rcn/ILoanManager.sol\n\npragma solidity ^0.6.6;\n\n\n\n\ninterface ILoanManager {\n    function token() external view returns (IERC20);\n\n    function debtEngine() external view returns (IDebtEngine);\n    function getCurrency(uint256 _id) external view returns (bytes32);\n    function getAmount(uint256 _id) external view returns (uint256);\n    function getAmount(bytes32 _id) external view returns (uint256);\n    function getOracle(uint256 _id) external view returns (address);\n\n    function settleLend(\n        bytes calldata _requestData,\n        bytes calldata _loanData,\n        address _cosigner,\n        uint256 _maxCosignerCost,\n        bytes calldata _cosignerData,\n        bytes calldata _oracleData,\n        bytes calldata _creatorSig,\n        bytes calldata _borrowerSig\n    ) external returns (bytes32 id);\n\n    function lend(\n        bytes32 _id,\n        bytes calldata _oracleData,\n        address _cosigner,\n        uint256 _cosignerLimit,\n        bytes calldata _cosignerData,\n        bytes calldata _callbackData\n    ) external returns (bool);\n\n}\n\n// File: contracts/interfaces/ITokenConverter.sol\n\npragma solidity ^0.6.6;\n\n\n\ninterface ITokenConverter {\n    function convertFrom(\n        IERC20 _fromToken,\n        IERC20 _toToken,\n        uint256 _fromAmount,\n        uint256 _minReceive\n    ) external payable returns (uint256 _received);\n\n    function convertTo(\n        IERC20 _fromToken,\n        IERC20 _toToken,\n        uint256 _toAmount,\n        uint256 _maxSpend\n    ) external payable returns (uint256 _spend);\n\n    function getPriceConvertFrom(\n        IERC20 _fromToken,\n        IERC20 _toToken,\n        uint256 _fromAmount\n    ) external view returns (uint256 _receive);\n\n    function getPriceConvertTo(\n        IERC20 _fromToken,\n        IERC20 _toToken,\n        uint256 _toAmount\n    ) external view returns (uint256 _spend);\n}\n\n// File: contracts/utils/SafeERC20.sol\n\npragma solidity ^0.6.6;\n\n\n\n/**\n* @dev Library to perform safe calls to standard method for ERC20 tokens.\n*\n* Why Transfers: transfer methods could have a return value (bool), throw or revert for insufficient funds or\n* unathorized value.\n*\n* Why Approve: approve method could has a return value (bool) or does not accept 0 as a valid value (BNB token).\n* The common strategy used to clean approvals.\n*\n* We use the Solidity call instead of interface methods because in the case of transfer, it will fail\n* for tokens with an implementation without returning a value.\n* Since versions of Solidity 0.4.22 the EVM has a new opcode, called RETURNDATASIZE.\n* This opcode stores the size of the returned data of an external call. The code checks the size of the return value\n* after an external call and reverts the transaction in case the return data is shorter than expected\n* https://github.com/nachomazzara/SafeERC20/blob/master/contracts/libs/SafeERC20.sol\n*/\nlibrary SafeERC20 {\n    /**\n    * @dev Transfer token for a specified address\n    * @param _token erc20 The address of the ERC20 contract\n    * @param _to address The address which you want to transfer to\n    * @param _value uint256 the _value of tokens to be transferred\n    * @return bool whether the transfer was successful or not\n    */\n    function safeTransfer(IERC20 _token, address _to, uint256 _value) internal returns (bool) {\n        uint256 prevBalance = _token.balanceOf(address(this));\n\n        if (prevBalance < _value) {\n            // Insufficient funds\n            return false;\n        }\n\n        (bool success,) = address(_token).call(\n            abi.encodeWithSignature(\"transfer(address,uint256)\", _to, _value)\n        );\n\n        if (!success || prevBalance - _value != _token.balanceOf(address(this))) {\n            // Transfer failed\n            return false;\n        }\n\n        return true;\n    }\n\n    /**\n    * @dev Transfer tokens from one address to another\n    * @param _token erc20 The address of the ERC20 contract\n    * @param _from address The address which you want to send tokens from\n    * @param _to address The address which you want to transfer to\n    * @param _value uint256 the _value of tokens to be transferred\n    * @return bool whether the transfer was successful or not\n    */\n    function safeTransferFrom(\n        IERC20 _token,\n        address _from,\n        address _to,\n        uint256 _value\n    ) internal returns (bool)\n    {\n        uint256 prevBalance = _token.balanceOf(_from);\n\n        if (prevBalance < _value) {\n            // Insufficient funds\n            return false;\n        }\n\n        if (_token.allowance(_from, address(this)) < _value) {\n            // Insufficient allowance\n            return false;\n        }\n\n        (bool success,) = address(_token).call(\n            abi.encodeWithSignature(\"transferFrom(address,address,uint256)\", _from, _to, _value)\n        );\n\n        if (!success || prevBalance - _value != _token.balanceOf(_from)) {\n            // Transfer failed\n            return false;\n        }\n\n        return true;\n    }\n\n   /**\n   * @dev Approve the passed address to spend the specified amount of tokens on behalf of msg.sender.\n   *\n   * Beware that changing an allowance with this method brings the risk that someone may use both the old\n   * and the new allowance by unfortunate transaction ordering. One possible solution to mitigate this\n   * race condition is to first reduce the spender\"s allowance to 0 and set the desired value afterwards:\n   * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729\n   *\n   * @param _token erc20 The address of the ERC20 contract\n   * @param _spender The address which will spend the funds.\n   * @param _value The amount of tokens to be spent.\n   * @return bool whether the approve was successful or not\n   */\n    function safeApprove(IERC20 _token, address _spender, uint256 _value) internal returns (bool) {\n        (bool success,) = address(_token).call(\n            abi.encodeWithSignature(\"approve(address,uint256)\",_spender, _value)\n        );\n\n        if (!success && _token.allowance(address(this), _spender) != _value) {\n            // Approve failed\n            return false;\n        }\n\n        return true;\n    }\n\n   /**\n   * @dev Clear approval\n   * Note that if 0 is not a valid value it will be set to 1.\n   * @param _token erc20 The address of the ERC20 contract\n   * @param _spender The address which will spend the funds.\n   */\n    function clearApprove(IERC20 _token, address _spender) internal returns (bool) {\n        bool success = safeApprove(_token, _spender, 0);\n\n        if (!success) {\n            success = safeApprove(_token, _spender, 1);\n        }\n\n        return success;\n    }\n}\n\n// File: contracts/utils/SafeTokenConverter.sol\n\npragma solidity ^0.6.6;\n\n\n\n\n\n\nlibrary SafeTokenConverter {\n    IERC20 constant private ETH_TOKEN_ADDRESS = IERC20(0x00eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee);\n    using SafeERC20 for IERC20;\n    using SafeMath for uint256;\n\n    function safeConvertFrom(\n        ITokenConverter _converter,\n        IERC20 _fromToken,\n        IERC20 _toToken,\n        uint256 _fromAmount,\n        uint256 _minReceive\n    ) internal returns (uint256 _received) {\n        uint256 prevBalance = _selfBalance(_toToken);\n\n        if (_fromToken == ETH_TOKEN_ADDRESS) {\n            _converter.convertFrom{\n                value: _fromAmount\n            }(\n                _fromToken,\n                _toToken,\n                _fromAmount,\n                _minReceive\n            );\n        } else {\n            require(_fromToken.safeApprove(address(_converter), _fromAmount), \"error approving converter\");\n            _converter.convertFrom(\n                _fromToken,\n                _toToken,\n                _fromAmount,\n                _minReceive\n            );\n\n            require(_fromToken.clearApprove(address(_converter)), \"error clearing approve\");\n        }\n\n        _received = _selfBalance(_toToken).sub(prevBalance);\n        require(_received >= _minReceive, \"_minReceived not reached\");\n    }\n\n    function safeConvertTo(\n        ITokenConverter _converter,\n        IERC20 _fromToken,\n        IERC20 _toToken,\n        uint256 _toAmount,\n        uint256 _maxSpend\n    ) internal returns (uint256 _spend) {\n        uint256 prevFromBalance = _selfBalance(_fromToken);\n        uint256 prevToBalance = _selfBalance(_toToken);\n\n        if (_fromToken == ETH_TOKEN_ADDRESS) {\n            _converter.convertTo{\n                value: _maxSpend\n            }(\n                _fromToken,\n                _toToken,\n                _toAmount,\n                _maxSpend\n            );\n        } else {\n            require(_fromToken.safeApprove(address(_converter), _maxSpend), \"error approving converter\");\n            _converter.convertTo(\n                _fromToken,\n                _toToken,\n                _toAmount,\n                _maxSpend\n            );\n\n            require(_fromToken.clearApprove(address(_converter)), \"error clearing approve\");\n        }\n\n        _spend = prevFromBalance.sub(_selfBalance(_fromToken));\n        require(_spend <= _maxSpend, \"_maxSpend exceeded\");\n        require(_selfBalance(_toToken).sub(prevToBalance) >= _toAmount, \"_toAmount not received\");\n    }\n\n    function _selfBalance(IERC20 _token) private view returns (uint256) {\n        if (_token == ETH_TOKEN_ADDRESS) {\n            return address(this).balance;\n        } else {\n            return _token.balanceOf(address(this));\n        }\n    }\n}\n\n// File: contracts/utils/Math.sol\n\npragma solidity ^0.6.6;\n\n\nlibrary Math {\n    function min(uint256 _a, uint256 _b) internal pure returns (uint256) {\n        if (_a < _b) {\n            return _a;\n        } else {\n            return _b;\n        }\n    }\n\n    function divCeil(uint256 _a, uint256 _b) internal pure returns (uint256 c) {\n        require(_b != 0, \"div by zero\");\n        c = _a / _b;\n        if (_a % _b != 0) {\n            c = c + 1;\n        }\n    }\n}\n\n// File: contracts/ConverterRamp.sol\n\npragma solidity ^0.6.6;\n\n\n\n\n\n\n\n\n\n\n\n\n\n/// @title  Converter Ramp\n/// @notice for conversion between different assets, use ITokenConverter\n///         contract as abstract layer for convert different assets.\n/// @dev All function calls are currently implemented without side effects\ncontract ConverterRamp is Ownable {\n    using SafeTokenConverter for ITokenConverter;\n    using SafeMath for uint256;\n    using SafeERC20 for IERC20;\n    using Math for uint256;\n\n    /// @notice address to identify operations with ETH\n    address public constant ETH_ADDRESS = address(0x00eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee);\n\n    event Return(address _token, address _to, uint256 _amount);\n    event ReadedOracle(address _oracle, uint256 _tokens, uint256 _equivalent);\n\n    IDebtEngine public debtEngine;\n    ILoanManager public loanManager;\n    IERC20 public token;\n\n    constructor(ILoanManager _loanManager) public {\n        loanManager = _loanManager;\n        token = _loanManager.token();\n        debtEngine = _loanManager.debtEngine();\n    }\n\n    function pay(\n        ITokenConverter _converter,\n        IERC20 _fromToken,\n        uint256 _payAmount,\n        uint256 _maxSpend,\n        bytes32 _requestId,\n        bytes calldata _oracleData\n    ) external payable {\n        IDebtEngine _debtEngine = debtEngine;\n\n        // Get amount required, in RCN, for payment\n        uint256 amount = getRequiredRcnPay(\n            _debtEngine,\n            _requestId,\n            _payAmount,\n            _oracleData\n        );\n\n        // Pull funds from sender\n        IERC20 _token = token;\n        _pullConvertAndReturnExtra(\n            _converter,\n            _fromToken,\n            _token,\n            amount,\n            _maxSpend\n        );\n\n        // Pay the loan the debtEngine is trusted so we can approve it only once\n        _approveOnlyOnce(_token, address(_debtEngine), amount);\n\n        // Execute the payment\n        (, uint256 paidToken) = _debtEngine.payToken(_requestId, amount, msg.sender, _oracleData);\n\n        // Convert any extra RCN and send it back it should not be reachable\n        if (paidToken < amount) {\n            _convertAndReturn(\n                _converter,\n                _token,\n                _fromToken,\n                amount - paidToken\n            );\n        }\n    }\n\n    function lend(\n        ITokenConverter _converter,\n        IERC20 _fromToken,\n        uint256 _maxSpend,\n        ICosigner _cosigner,\n        uint256 _cosignerLimitCost,\n        bytes32 _requestId,\n        bytes memory _oracleData,\n        bytes memory _cosignerData,\n        bytes memory _callbackData\n    ) public payable {\n        ILoanManager _loanManager = loanManager;\n\n        // Get required RCN for lending the loan\n        uint256 amount = getRequiredRcnLend(\n            _loanManager,\n            _cosigner,\n            _requestId,\n            _oracleData,\n            _cosignerData\n        );\n\n        IERC20 _token = token;\n        _pullConvertAndReturnExtra(\n            _converter,\n            _fromToken,\n            _token,\n            amount,\n            _maxSpend\n        );\n\n        // Approve token to loan manager only once the loan manager is trusted\n        _approveOnlyOnce(_token, address(_loanManager), amount);\n\n        _loanManager.lend(\n            _requestId,\n            _oracleData,\n            address(_cosigner),\n            _cosignerLimitCost,\n            _cosignerData,\n            _callbackData\n        );\n\n        // Transfer loan to the msg.sender\n        debtEngine.transferFrom(address(this), msg.sender, uint256(_requestId));\n    }\n\n    function getLendCost(\n        ITokenConverter _converter,\n        IERC20 _fromToken,\n        ICosigner _cosigner,\n        bytes32 _requestId,\n        bytes calldata _oracleData,\n        bytes calldata _cosignerData\n    ) external returns (uint256) {\n        uint256 amountRcn = getRequiredRcnLend(\n            loanManager,\n            _cosigner,\n            _requestId,\n            _oracleData,\n            _cosignerData\n        );\n\n        return _converter.getPriceConvertTo(\n            _fromToken,\n            token,\n            amountRcn\n        );\n    }\n\n    /// @notice returns how much RCN is required for a given pay\n    function getPayCost(\n        ITokenConverter _converter,\n        IERC20 _fromToken,\n        bytes32 _requestId,\n        uint256 _amount,\n        bytes calldata _oracleData\n    ) external returns (uint256) {\n        uint256 amountRcn = getRequiredRcnPay(\n            debtEngine,\n            _requestId,\n            _amount,\n            _oracleData\n        );\n\n        return _converter.getPriceConvertTo(\n            _fromToken,\n            token,\n            amountRcn\n        );\n    }\n\n    /// @notice returns how much RCN is required for a given lend\n    function getRequiredRcnLend(\n        ILoanManager _loanManager,\n        ICosigner _lenderCosignerAddress,\n        bytes32 _requestId,\n        bytes memory _oracleData,\n        bytes memory _cosignerData\n    ) internal returns (uint256) {\n        // Load request amount\n        uint256 amount = loanManager.getAmount(_requestId);\n\n        // If loan has a cosigner, sum the cost\n        if (_lenderCosignerAddress != ICosigner(0)) {\n            amount = amount.add(\n                _lenderCosignerAddress.cost(\n                    address(_loanManager),\n                    uint256(_requestId),\n                    _cosignerData,\n                    _oracleData\n                )\n            );\n        }\n\n        // Load the  Oracle rate and convert required\n        address oracle = loanManager.getOracle(uint256(_requestId));\n\n        return getCurrencyToToken(oracle, amount, _oracleData);\n    }\n\n    /// @notice returns how much RCN is required for a given pay\n    function getRequiredRcnPay(\n        IDebtEngine _debtEngine,\n        bytes32 _requestId,\n        uint256 _amount,\n        bytes memory _oracleData\n    ) internal returns (uint256 _result) {\n        (,,IModel model,, IRateOracle oracle) = _debtEngine.debts(_requestId);\n\n        // Load amount to pay\n        uint256 amount = Math.min(\n            model.getClosingObligation(_requestId),\n            _amount\n        );\n\n        // Read loan oracle\n        return getCurrencyToToken(address(oracle), amount, _oracleData);\n    }\n\n    /// @notice returns how much tokens for _amount currency\n    /// @dev tokens and equivalents get oracle data\n    function getCurrencyToToken(\n        address _oracle,\n        uint256 _amount,\n        bytes memory _oracleData\n    ) internal returns (uint256) {\n        if (_oracle == address(0)) {\n            return _amount;\n        }\n\n        (uint256 tokens, uint256 equivalent) = IRateOracle(_oracle).readSample(_oracleData);\n\n        emit ReadedOracle(_oracle, tokens, equivalent);\n        return tokens.mul(_amount).divCeil(equivalent);\n    }\n\n    function getPriceConvertTo(\n        ITokenConverter _converter,\n        IERC20 _fromToken,\n        uint256 _amount\n    ) external view returns (uint256) {\n        return _converter.getPriceConvertTo(\n            _fromToken,\n            token,\n            _amount\n        );\n    }\n\n    function _convertAndReturn(\n        ITokenConverter _converter,\n        IERC20 _fromToken,\n        IERC20 _toToken,\n        uint256 _amount\n    ) private {\n        uint256 buyBack = _converter.safeConvertFrom(\n            _fromToken,\n            _toToken,\n            _amount,\n            1\n        );\n\n        require(_toToken.safeTransfer(msg.sender, buyBack), \"error sending extra\");\n    }\n\n    function _pullConvertAndReturnExtra(\n        ITokenConverter _converter,\n        IERC20 _fromToken,\n        IERC20 _toToken,\n        uint256 _amount,\n        uint256 _maxSpend\n    ) private {\n        // Pull limit amount from sender\n        _pull(_fromToken, _maxSpend);\n\n        uint256 spent = _converter.safeConvertTo(_fromToken, _toToken, _amount, _maxSpend);\n\n        if (spent < _maxSpend) {\n            _transfer(_fromToken, msg.sender, _maxSpend - spent);\n        }\n    }\n\n    function _pull(\n        IERC20 _token,\n        uint256 _amount\n    ) private {\n        if (address(_token) == ETH_ADDRESS) {\n            require(msg.value == _amount, \"sent eth is not enought\");\n        } else {\n            require(msg.value == 0, \"method is not payable\");\n            require(_token.safeTransferFrom(msg.sender, address(this), _amount), \"error pulling tokens\");\n        }\n    }\n\n    function _transfer(\n        IERC20 _token,\n        address payable _to,\n        uint256 _amount\n    ) private {\n        if (address(_token) == ETH_ADDRESS) {\n            _to.transfer(_amount);\n        } else {\n            require(_token.safeTransfer(_to, _amount), \"error sending tokens\");\n        }\n    }\n\n    function _approveOnlyOnce(\n        IERC20 _token,\n        address _spender,\n        uint256 _amount\n    ) private {\n        uint256 allowance = _token.allowance(address(this), _spender);\n        if (allowance < _amount) {\n            if (allowance != 0) {\n                _token.clearApprove(_spender);\n            }\n\n            _token.approve(_spender, uint(-1));\n        }\n    }\n\n    function emergencyWithdraw(\n        IERC20 _token,\n        address _to,\n        uint256 _amount\n    ) external onlyOwner {\n        _token.transfer(_to, _amount);\n    }\n\n    receive() external payable {\n        // solhint-disable-next-line\n        require(tx.origin != msg.sender, \"ramp: send eth rejected\");\n    }\n}"
    }
  }
}